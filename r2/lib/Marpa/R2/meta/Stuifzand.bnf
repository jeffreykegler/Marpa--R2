# Copyright 2012 Jeffrey Kegler
# This file is part of Marpa::R2.  Marpa::R2 is free software: you can
# redistribute it and/or modify it under the terms of the GNU Lesser
# General Public License as published by the Free Software Foundation,
# either version 3 of the License, or (at your option) any later version.
#
# Marpa::R2 is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
# Lesser General Public License for more details.
#
# You should have received a copy of the GNU Lesser
# General Public License along with Marpa::R2.  If not, see
# http://www.gnu.org/licenses/.

:start ::= rules
rules ::= rule+
rule ::= start_rule | empty_rule | priority_rule | quantified_rule | discard_rule
start_rule ::= (<kwc start> op_declare) symbol
priority_rule ::= lhs op_declare priorities
empty_rule ::= lhs op_declare adverb_list
quantified_rule ::= lhs op_declare single_symbol quantifier adverb_list
discard_rule ::= (<kwc discard> op_declare_match) single_symbol
op_declare ::= op_declare_bnf | op_declare_match
priorities ::= alternatives+
    separator => op_tighter proper => 1
alternatives ::= alternative+
    separator => op_eq_pri proper => 1
alternative ::= rhs adverb_list
adverb_list ::= adverb_item*
adverb_item ::=
      action
    | left_association | right_association | group_association
    | separator_specification | proper_specification

action ::= (kw_action op_arrow) action_name
left_association ::= (kw_assoc op_arrow kw_left)
right_association ::= (kw_assoc op_arrow kw_right)
group_association ::= (kw_assoc op_arrow kw_group)
separator_specification ::= (kw_separator op_arrow) single_symbol
proper_specification ::= (kw_proper op_arrow) boolean

quantifier ::=
    op_star
  | op_plus

lhs ::= symbol_name
rhs ::= rhs_primary+
rhs_primary ::= single_symbol
rhs_primary ::= <kwc ws_star>
rhs_primary ::= <kwc ws_plus>
rhs_primary ::= <kwc ws>
rhs_primary ::= <kwc any>
rhs_primary ::= single_quoted_string
rhs_primary ::= <parenthesized rhs primary list>
<parenthesized rhs primary list> ::= (op_lparen) rhs_primary_list (op_rparen)
rhs_primary_list ::= rhs_primary+
single_symbol ::=
    symbol
  | character_class
symbol ::= symbol_name
symbol_name ::= bare_name | reserved_word
symbol_name ::= bracketed_name
action_name ::= bare_name | reserved_action_name

reserved_word ::= kw_action | kw_assoc | kw_separator | kw_proper
  | kw_left | kw_right | kw_group
