# Copyright 2012 Jeffrey Kegler
# This file is part of Marpa::R2.  Marpa::R2 is free software: you can
# redistribute it and/or modify it under the terms of the GNU Lesser
# General Public License as published by the Free Software Foundation,
# either version 3 of the License, or (at your option) any later version.
#
# Marpa::R2 is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
# Lesser General Public License for more details.
#
# You should have received a copy of the GNU Lesser
# General Public License along with Marpa::R2.  If not, see
# http://www.gnu.org/licenses/.


rules ::= rule+ action => do_rules
rule ::= start_rule | empty_rule | priority_rule | quantified_rule
start_rule ::= (kw__start) op_declare symbol_name
  action => do_start_rule
priority_rule ::= lhs op_declare priorities
  action => do_priority_rule
empty_rule ::= lhs op_declare adverb_list
  action => do_empty_rule
quantified_rule ::= lhs op_declare single_symbol quantifier adverb_list
    action => do_quantified_rule
op_declare ::= 
      op_declare_bnf action => do_op_declare_bnf
    | op_declare_match action => do_op_declare_match
priorities ::= alternatives+
    separator => op_tighter proper => 1
    action => do_discard_separators
alternatives ::= alternative+
    separator => op_eq_pri proper => 1
    action => do_discard_separators
alternative ::= rhs adverb_list action => do_alternative 
adverb_list ::= adverb_item* action => do_adverb_list
adverb_item ::=
      action
    | left_association | right_association | group_association
    | separator_specification | proper_specification

action ::= (kw_action op_arrow) action_name action => do_action
left_association ::= kw_assoc op_arrow kw_left
  action => do_left_association 
right_association ::= kw_assoc op_arrow kw_right
  action => do_right_association 
group_association ::= kw_assoc op_arrow kw_group
  action => do_group_association 
separator_specification ::= kw_separator op_arrow single_symbol
  action => do_separator_specification
proper_specification ::= kw_proper op_arrow boolean
action => do_proper_specification

quantifier ::=
    op_star action => do_op_star_quantifier
  | op_plus action => do_op_plus_quantifier

lhs ::= symbol_name action => do_lhs 
rhs ::= rhs_primary+ action => do_rhs
rhs_primary ::= single_symbol action => do_symbol_list
rhs_primary ::= kw__ws_star action => do_ws_star
rhs_primary ::= kw__ws_plus action => do_ws_plus
rhs_primary ::= kw__ws action => do_ws
rhs_primary ::= kw__any action => do_any
rhs_primary ::= kw__end_of_input action => do_end_of_input
rhs_primary ::= single_quoted_string action => do_single_quoted_string
rhs_primary ::= op_lparen rhs_primary_list op_rparen
  action => do_parenthesized_symbol_list
rhs_primary_list ::= rhs_primary+ action => do_symbol_list
single_symbol ::=
    symbol
  | character_class action => do_character_class
symbol ::= symbol_name action => do_symbol
symbol_name ::= bare_name | reserved_word
symbol_name ::= bracketed_name action => do_bracketed_name
action_name ::= bare_name | reserved_action_name

reserved_word ::= kw_action | kw_assoc | kw_separator | kw_proper
  | kw_left | kw_right | kw_group
