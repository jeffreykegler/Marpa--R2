#!/usr/bin/env perl

use 5.010;
use strict;
use warnings FATAL => 'all';
use autodie;
use English qw( -no_match_vars );
use IPC::Cmd;

chomp( my $LATEST =
        qx(grep '^[0-9]' cpan/Changes | head -1 | awk '{print \$1}') );

warn "Bumping -> $LATEST\n";

my ( $major, $minor, $micro ) = ( $LATEST =~ m/\A (\d+) [.] (\d\d\d) [_]? (\d\d\d) /xms );
if ( not defined $micro ) {
    die "bump_version Cannot handle new version number: $LATEST";
}
$major += 0;
$minor += 0;
$micro += 0;
my $libmarpa_major = $major+3;
my $libmarpa_minor = $minor+100;
my $libmarpa_micro = $micro+100;

my @versioned_files = split /\n/xms, <<'END_OF_LIST';
cpan/html/lib/Marpa/R2/HTML.pm
cpan/html/lib/Marpa/R2/HTML.pm
cpan/html/lib/Marpa/R2/HTML/Callback.pm
cpan/html/lib/Marpa/R2/HTML/Config.pm
cpan/html/lib/Marpa/R2/HTML/Config/Compile.pm
cpan/html/meta/make_internal_pm.pl
cpan/html/tool/lib/Marpa/R2/HTML/Test/Util.pm
cpan/lib/Marpa/R2/ASF.pm
cpan/lib/Marpa/R2/Grammar.pm
cpan/lib/Marpa/R2/MetaAST.pm
cpan/lib/Marpa/R2/MetaG.pm
cpan/lib/Marpa/R2.pm
cpan/lib/Marpa/R2/Recognizer.pm
cpan/lib/Marpa/R2/SLG.pm
cpan/lib/Marpa/R2/SLR.pm
cpan/lib/Marpa/R2/Stuifzand.pm
cpan/lib/Marpa/R2/Thin/Trace.pm
cpan/lib/Marpa/R2/Value.pm
cpan/meta/make_internal_pm.pl
END_OF_LIST

for my $versioned_file (@versioned_files) {
    say STDERR $versioned_file;
    my $file = do { local ( @ARGV, $/ ) = ($versioned_file); <> };
    $file =~ s/(\$VERSION \s+ =  \s+ ['] ) \d+ [.] \d\d\d [_]? \d\d\d /$1${LATEST}/xms;
    open my $out, '>', $versioned_file;
    print $out $file;
} ## end for my $versioned_file (@versioned_files)

{
    # Update the version of Libmarpa that XS is expecting
    my $expecting_file = 'cpan/xs/R2.xs';
    say STDERR $expecting_file;
    my $file = do { local ( @ARGV, $/ ) = ($expecting_file); <> };
    $file =~ s{
    ^ [#]define \s+ EXPECTED_LIBMARPA_MAJOR \s [^\n]* $}
    {#define EXPECTED_LIBMARPA_MAJOR $libmarpa_major}xms;
    $file =~ s{
    ^ [#]define \s+ EXPECTED_LIBMARPA_MINOR \s [^\n]* $}
    {#define EXPECTED_LIBMARPA_MINOR $libmarpa_minor}xms;
    $file =~ s{
    ^ [#]define \s+ EXPECTED_LIBMARPA_MICRO \s [^\n]* $}
    {#define EXPECTED_LIBMARPA_MICRO $libmarpa_micro}xms;
    open my $out, '>', $expecting_file;
    print $out $file;
}

for my $configure_ac_file (
    qw(
    cpan/libmarpa/ac/configure.ac
    cpan/libmarpa/ac_doc/configure.ac
    )
    )
{
    say STDERR $configure_ac_file;
    my $file = do { local ( @ARGV, $/ ) = ($configure_ac_file); <> };
    $file =~ s{
    ( m4_define [(] [\[] marpa_major_version [\]] [,] \s* [\[] )  \d+ ( [\]] )
  }{$1${libmarpa_major}$2}xms;
    $file =~ s{
    ( m4_define [(] [\[] marpa_minor_version [\]] [,] \s* [\[] )  \d+ ( [\]] )
  }{$1${libmarpa_minor}$2}xms;
    $file =~ s{
    ( m4_define [(] [\[] marpa_micro_version [\]] [,] \s* [\[] )  \d+ ( [\]] )
  }{$1${libmarpa_micro}$2}xms;
    open my $out, '>', $configure_ac_file;
    print $out $file;
} ## end for my $configure_ac_file ( qw( cpan/libmarpa/dev/dist/configure.ac...))

chdir 'cpan';
die qq{"perl Build.PL" failed: $!}
    if not IPC::Cmd::run(
            command => [ $EXECUTABLE_NAME, 'Build.PL' ],
            verbose => 1
    );
die qq{"./Build distmeta" failed: $!}
    if
    not IPC::Cmd::run( command => [ './Build', 'distmeta' ], verbose => 1 );
chdir 'meta';
die qq{"make all" in cpan/meta/ directory failed: $!}
    if
    not IPC::Cmd::run( command => [ 'make', 'all' ], verbose => 1 );
